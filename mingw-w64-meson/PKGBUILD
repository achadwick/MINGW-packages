# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=meson
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.37.1
pkgrel=2
pkgdesc="High-productivity build system (mingw-w64)"
arch=('any')
url="http://mesonbuild.com/"
license=("Apache 2")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-python3"
         "${MINGW_PACKAGE_PREFIX}-ninja")
source=(https://github.com/mesonbuild/${_realname}/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz)
sha256sums=('72516e25eaf9efd67fe8262ccba05e1e84731cc139101fcda7794aed9f68f55a')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PYTHON=${MINGW_PREFIX}/bin/python3 ${MINGW_PREFIX}/bin/python3 setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py \
    install --prefix=${MINGW_PREFIX} --root="${pkgdir}"

  # Fix up Python scripts and add convenience bash launchers for them.
  # Meson people expect to call it as "meson" in POSIX-like bash environments.
  PREFIX_WIN=$(cygpath -m ${MINGW_PREFIX})
  for p in "${pkgdir}${MINGW_PREFIX}"/bin/*.py; do
    sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i "$p"
    d=$(dirname "$p")
    b=$(basename "$p" .py)
    if test -e "$d/$b"; then
      echo >&2 "FIXME: $b is created upstream now, update package()"
    else
      cat >"$d/$b" <<"__END_STUB__"
#!/usr/bin/env bash
d=`dirname ${BASH_SOURCE[0]}`
b=`basename ${BASH_SOURCE[0]}`
exec "${d}/${b}.py" "$@"
__END_STUB__
    fi
  done

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
